/*
 * European Scoring Tool For Baseball
 * Copyright (C) 2020  Patrick BRIAND
 * 
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 *
 * generated by Xtext 2.23.0
 */
grammar org.bpy.score.game.Game with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
//import "http://www.bpy.org/score/club/Club" as teamDescription

generate game "http://www.bpy.org/score/game/Game"

/**
 * Entry point of the file
 */
Game:
	'game' name=STRING '{'
		description=Description
		visitor = Roster
		hometeam = Roster
		(
			(forfeitGame = ForfeitGame) |
			( actions = Actions ) 
	    )
     '}'
;

/**
 * Forfeit game description
 */
ForfeitGame:
	'forfeit' team=TeamTypeEnum ';'
;

/**
 * Describe the game (starting lineup, all the halfinning and the game resume 
 */
Actions:
	'gameEntry' '{'
		(
		    lineup += LineUp+
	        halfInnings += HalfInning*
	        (gameResume = GameResume)?
		)
	'}'
;

/**
 * Allow to define the winner pitcher, the looser pitcher and optionally the saved pitcher 
 */
GameResume:
	'gameResume' '{'
		'win' '=' win=STRING ';'
		'lose' '=' lose=STRING ';'
		('save' '=' save=STRING ';')?
	'}'
;

/**
 * Describe an half inning
 */
HalfInning:
	'inning' team=TeamTypeEnum 'number' number=INT '{'
		   inningSetting += InningSetting*
		   (
		   	  (inningElements += SubstitutionEvent) |
		   	  (inningElements += Action) |
		   	  (inningElements += Commands) |
		   	  (notes += Note) 
		   )*
	'}'
;

/**
 * Specific graphicals command
 */
Commands:
	'graphicalCommand' command=(MoveCommand | SelectedBatterCommand) 
;

/**
 * Allow to change the batter number
 */
MoveCommand:
	'columnNumber' '=' columnNumber=INT ';'  
;

/**
 * Select the current batter
 */
SelectedBatterCommand:
	'selectBatter' '=' starterBatter=INT ';'	
;

/**
 * Add a note 
 */
Note:
	'note' '=' notes=STRING ';'
;

/**
 * Specials case during an half inning
 */
InningSetting:
	{InningSetting}
	'specialSituation' '{'
		(isTiebrak ?= 'tieBreak' ) ?
	'}'
;

/**
 * Describe the actions in a half inning
 */
Action:
	(pitches = Pitches)?
 	'action' '{'
		(
	   		(batterAction   = BatterAction (',' runnerActions+=RunnerAction)* ) |
	   	    (runnerActions += RunnerAction (',' runnerActions+=RunnerAction)* )
   	    )
	'}'
;

/**
 * Allow to count the pitchs
 */
Pitches :
	pitches += Pitch (',' pitches += Pitch)*
;

/**
 * Type of picth
 */
Pitch:
	pitch = TypeOfPitch
;

/**
 * Define possible type of pitch
 */
TypeOfPitch:
	KindOfPitch | INT
;

RunnerAction:
	(RunnerAdvance moreAdvances+=AdvanceWithContinuation* (runnerOut = MoreAdanceFail)?) |
	(RunnerOut) |
	(OtherRunnerAction)
;

/**
 * Runner actions which doesn't generate an advance
 */
OtherRunnerAction:
	'noAdvance' 'on'
	(
	   ({RunnerMustBeOutOnError} runner=CurrentRunner EXECUTE actionCode=MUST_BE_OUT_ON_ERROR ) |
	   ({RunnerDontAdvanceOnError} runner=CurrentRunner EXECUTE actionCode=ADVANCE_ON_ERROR ) |
	   ({RunnerDontAdvanceOnPickOffWithError} runner=CurrentRunner EXECUTE actionCode=PICK_OFF_WITH_ERROR ) |
	   ({RunnerDontAdvanceOnCaughtStealingWithError} runner=CurrentRunner EXECUTE actionCode=CAUGTH_STEALING_WITH_ERROR ) |
	   ({RunnerDontAdvanceOnThrowError} runner=CurrentRunner EXECUTE actionCode=REACH_ON_THROW_ERROR ) |
	   ({RunnerDontAdvanceOnReceiveError} runner=CurrentRunner EXECUTE actionCode=REACH_ON_RECEIVE_ERROR ) |
	   ({RunnerDontAdvanceOnNonDecisiveThrowError}  runner=CurrentRunner EXECUTE actionCode= EXTRA_ON_THROW_ERROR ) |
	   ({RunnerDontAdvanceOnNonDecisiveReceiveError}  runner=CurrentRunner EXECUTE actionCode= EXTRA_ON_RECEIVE_ERROR )
   )
;

/**
 * Possible put out runner action
 */
RunnerOut:
	(({RunnerOutOnCaugthStealing} runner=CurrentRunner EXECUTE runnerOut=CAUGTH_STEALING) |
	({RunnerOutOnPickOff} runner=CurrentRunner EXECUTE runnerOut=PICK_OFF) |
	({RunnerOutOnFielderAction} runner=CurrentRunner EXECUTE runnerOut=INT) |
	({RunnerOutByRules} runner=CurrentRunner EXECUTE runnerOut=OUT_BY_RULE) |
	({RunnerOutOnAppeal} runner=CurrentRunner EXECUTE runnerOut=APPEAL))
	notInDoublePlay ?='dontCountAsDoublePlay'?
;

/**
 * Possible runner advance action
 */
RunnerAdvance:
	({RunnerAdvanceByBatterAction} runner=CurrentRunner EXECUTE runnerAdvance=ADVANCE (isEarned=('earned' | 'unEarnedForTeam'))?) |
	({RunnerAdvanceOnError} runner=CurrentRunner EXECUTE runnerAdvance=ADVANCE_ON_ERROR (isEarned=('earned' | 'unEarnedForTeam'))?) |
	({RunnerAdvanceOnStolenBase} runner=CurrentRunner EXECUTE runnerAdvance=STOLEN_BASE (isEarned=('earned' | 'unEarnedForTeam'))?) |
	({RunnerAdvanceOnWildPitch} runner=CurrentRunner EXECUTE runnerAdvance=WILD_PITCH (isEarned=('earned' | 'unEarnedForTeam'))?) |
	({RunnerAdvanceOnPassBall} runner=CurrentRunner EXECUTE runnerAdvance=PASS_BALL (isEarned=('earned' | 'unEarnedForTeam'))?) |
	({RunnerAdvanceOnBalk} runner=CurrentRunner EXECUTE runnerAdvance=BALK (isEarned=('earned' | 'unEarnedForTeam'))?) |
	({RunnerAdvanceOnPickOffWithError} runner=CurrentRunner EXECUTE runnerAdvance=PICK_OFF_WITH_ERROR (isEarned=('earned' | 'unEarnedForTeam'))?) |
	({RunnerAdvanceOnCaughtStealingWithError} runner=CurrentRunner EXECUTE runnerAdvance=CAUGTH_STEALING_WITH_ERROR (isEarned=('earned' | 'unEarnedForTeam'))?) |
	({RunnerAdvanceOnOccupedBall} runner=CurrentRunner EXECUTE runnerAdvance=FIELDER_CHOICE_WITH_PUTOUT (isEarned=('earned' | 'unEarnedForTeam'))?) |
   	({RunnerAdvanceOnNoDecisiveObstruction} runner=CurrentRunner EXECUTE  runnerAdvance=NO_DECISIVE_OBSTRUCTION (isEarned=('earned' | 'unEarnedForTeam'))?) |
   	({RunnerAdvanceOnDecisiveObstruction} runner=CurrentRunner EXECUTE runnerAdvance=DECISIVE_OBSTRUCTION (isEarned=('earned' | 'unEarnedForTeam'))?) |
	({RunnerAdvanceOnThrow} runner=CurrentRunner EXECUTE runnerAdvance=ADVANCE_ON_THROW (isEarned=('earned' | 'unEarnedForTeam'))?) |
	({RunnerAdvanceOnIndiference} runner=CurrentRunner EXECUTE runnerAdvance=INDIFERENCE (isEarned=('earned' | 'unEarnedForTeam'))?)  |
	({RunnerAdvanceOnThrowError} runner=CurrentRunner EXECUTE runnerAdvance=REACH_ON_THROW_ERROR (isEarned=('earned' | 'unEarnedForTeam'))?) |
	({RunnerAdvanceOnReceiveError} runner=CurrentRunner EXECUTE runnerAdvance=REACH_ON_RECEIVE_ERROR (isEarned=('earned' | 'unEarnedForTeam'))?) |
	({RunnerAdvanceOnNonDecisiveThrowError}  runner=CurrentRunner EXECUTE runnerAdvance= EXTRA_ON_THROW_ERROR (isEarned=('earned' | 'unEarnedForTeam'))?) |
	({RunnerAdvanceOnNonDecisiveReceiveError}  runner=CurrentRunner EXECUTE runnerAdvance= EXTRA_ON_RECEIVE_ERROR (isEarned=('earned' | 'unEarnedForTeam'))?) |
	({RunnerAdvanceOnRule}  runner=CurrentRunner EXECUTE runnerAdvance= ADVANCE_BY_RULE (isEarned=('earned' | 'unEarnedForTeam'))?) |
	({RunnerAdvanceOnOtherPlayerError}  runner=CurrentRunner EXECUTE runnerAdvance= ADVANCE_BY_OTHER_PLAYER_EROR (isEarned=('earned' | 'unEarnedForTeam'))?) |
	({RunnerAdvanceOnFielderChoice} runner=CurrentRunner EXECUTE runnerAdvance= FIELDER_CHOICE (isEarned=('earned' | 'unEarnedForTeam'))?)
;

/**
 * Manage the substitution
 */
SubstitutionEvent:
	'substitution' team=TeamTypeEnum
	(
	  '{'
		substitutions += SubstitutionDescription  ( ',' substitutions += SubstitutionDescription)*
	  '}'
	)  |
	(
	   substitutions += SubstitutionDescription ';'
   )
;

/**
 * Description of a substitution
 */
SubstitutionDescription:
	'player'
	    currentDefensivePosition = LineUPCurrentDefensivePosition ('(' name=STRING ')')?
	    (
	    	 (subtitution = Replacement) |
	         (subtitution = NewDefensivePosition)
	    )
;

/**
 * defensive substitution description
 */
NewDefensivePosition:
	'moveTo' newDefensivePosition = LineUPAtDefensivePosition ('laterality' '=' laterality=LATERALITY)?
;

/**
 * Offensive subtitiion description
 */
Replacement:
	'replaceBy' ( (name=STRING  ('shirtNumber' '=' shirtNumber=INT)?) | (defensivePosition=INT) ) ('at' newDefensivePosition = LineUPAtDefensivePosition)? ('laterality' '=' laterality=LATERALITY)? 
;

/**
 * batter action
 */
BatterAction:
	BatterOut  |
	BatterAdvance |
	BatterLostTurn |
	BatterMustOutOnFlyFoulBall |
	BatterBalk
;

/**
 * Balk batter action
 */
BatterBalk:
	{BatterBalk} currentBatter=CurrentBatter EXECUTE batterAction=BALK
;

/**
 * Batter which must be out on bad catch in the fall ball area 
 */
BatterMustOutOnFlyFoulBall:
	{BatterMustOutOnFlyFoulBall} currentBatter=CurrentBatter EXECUTE error=MUST_BE_OUT_ON_ERROR
;

/**
 * Batter lost turn
 */
BatterLostTurn:
	{BatterLostTurn} currentBatter=CurrentBatter EXECUTE batterAdvance=LOST_TURN
;

/**
 * All type of batter advance
 */
BatterAdvance:
	(
		( Hit |
		  DecisiveError |
	      OtherBatterAdvance  |
	      DefensiveChoiceForBatter
	    )
	    moreAdvances+=AdvanceWithContinuation*
	    (out = MoreAdanceFail)?
    )
;

/**
 * Batter advance on defensive choice action
 */
DefensiveChoiceForBatter:
	({BatterAdvanceOnOccupedBall} currentBatter=CurrentBatter EXECUTE batterAdvance=FIELDER_CHOICE_WITH_PUTOUT) |
	({BatterAdvanceOnDefensiveChoice} currentBatter=CurrentBatter EXECUTE batterAdvance=FIELDER_CHOICE) |
	({BatterAdvanceOnIndiference} currentBatter=CurrentBatter EXECUTE batterAdvance=INDIFERENCE)
 ;

/**
 * More advance possible action
 */
MoreAdanceFail:
	({RunnerOutOnFielderAction} runnerOut=INT) |
	({RunnerOutByRules} runnerOut=OUT_BY_RULE) |
	({RunnerOutOnAppeal} runnerOut=APPEAL)
;

/**
 * Advance from continuation for runner action
 */
AdvanceWithContinuation:
	(
		({RunnerAdvanceOnNonDecisiveFlyError}  runnerAdvance=EXTRA_ON_FLY_ERROR (isEarned=('earned' | 'unEarnedForTeam'))?) |
 		({RunnerAdvanceOnError} runnerAdvance=ADVANCE_ON_ERROR (isEarned=('earned' | 'unEarnedForTeam'))?) |
		({RunnerAdvanceOnNonDecisiveThrowError}  runnerAdvance=EXTRA_ON_THROW_ERROR (isEarned=('earned' | 'unEarnedForTeam'))?) |
		({RunnerAdvanceOnNonDecisiveReceiveError}  runnerAdvance=EXTRA_ON_RECEIVE_ERROR (isEarned=('earned' | 'unEarnedForTeam'))?) |
		({RunnerAdvanceOnOccupedBall} runnerAdvance=FIELDER_CHOICE_WITH_PUTOUT (isEarned=('earned' | 'unEarnedForTeam'))?) |
	    ({RunnerAdvanceOnDefensiveChoice} runnerAdvance=FIELDER_CHOICE (isEarned=('earned' | 'unEarnedForTeam'))?) |
		({RunnerAdvanceOnThrowError} runnerAdvance=REACH_ON_THROW_ERROR (isEarned=('earned' | 'unEarnedForTeam'))?) |
		({RunnerAdvanceOnReceiveError} runnerAdvance=REACH_ON_RECEIVE_ERROR (isEarned=('earned' | 'unEarnedForTeam'))?) |
     	({RunnerAdvanceOnThrow} runnerAdvance=ADVANCE_ON_THROW (isEarned=('earned' | 'unEarnedForTeam'))?) |
     	({RunnerAdvanceOnNoDecisiveObstruction} runnerAdvance=NO_DECISIVE_OBSTRUCTION (isEarned=('earned' | 'unEarnedForTeam'))?) |
     	({RunnerAdvanceOnDecisiveObstruction} runnerAdvance=DECISIVE_OBSTRUCTION (isEarned=('earned' | 'unEarnedForTeam'))?) |
     	({RunnerAdvanceOnRule} runnerAdvance=ADVANCE_BY_RULE (isEarned=('earned' | 'unEarnedForTeam'))?) |
     	({RunnerAdvanceOnIndiference} runnerAdvance=INDIFERENCE (isEarned=('earned' | 'unEarnedForTeam'))?) |
     	({RunnerAdvanceOnWildPitch} runnerAdvance=WILD_PITCH (isEarned=('earned' | 'unEarnedForTeam'))?) |
     	({RunnerAdvanceOnPassBall} runnerAdvance=PASS_BALL (isEarned=('earned' | 'unEarnedForTeam'))?) |
    	({RunnerAdvanceOnBalk} runnerAdvance=BALK (isEarned=('earned' | 'unEarnedForTeam'))?) |
     	({RunnerAdvanceOnStolenBase} runnerAdvance=STOLEN_BASE (isEarned=('earned' | 'unEarnedForTeam'))?)
	)
;

/**
 * OTher batter advance  (specific cases)
 */
OtherBatterAdvance:
	({BatterAdvanceOnBaseOnBall} currentBatter=CurrentBatter EXECUTE batterAdvance=BASE_ON_BALL ) |
	({BatterAdvanceOnIntentionalBaseOnBall} currentBatter=CurrentBatter EXECUTE batterAdvance=INTENTIONAL_BASE_ON_BALL ) |
	({BatterAdvanceOnHitByPitch} currentBatter=CurrentBatter EXECUTE batterAdvance=HIT_BY_PITCH ) |
	({BatterAdvanceOnKPassBall} currentBatter=CurrentBatter EXECUTE batterAdvance=K_PASS_BALL ) |
	({BatterAdvanceOnKWildPitch} currentBatter=CurrentBatter EXECUTE batterAdvance=K_WILD_PITCH ) |
	({BatterAdvanceOnKAbr} currentBatter=CurrentBatter EXECUTE batterAdvance=K_ADVANCE_BY_RULE ) |
	({BatterAdvanceOnKWithError} currentBatter=CurrentBatter EXECUTE batterAdvance=K_WITH_ERROR ) |
	({BatterAdvanceOnSacrificeHitWithError} currentBatter=CurrentBatter EXECUTE batterAdvance=SACRIFICE_BUNT_WITH_ERROR ) |
	({BatterAdvanceOnSacrificeHitWithFielderChoice} currentBatter=CurrentBatter EXECUTE batterAdvance=SACRIFICE_BUNT_WITH_FIELDER_CHOICE ) |
	({BatterAdvanceOnSacrificeFlyWithError} currentBatter=CurrentBatter EXECUTE batterAdvance=SACRIFICE_FLY_WITH_ERROR ) |
	({BatterAdvanceOnSacrificeFlyWithFielderChoice} currentBatter=CurrentBatter EXECUTE batterAdvance=SACRIFICE_FLY_WITH_FIELDER_CHOICE ) |
	({BatterAdvanceOnCatcherInterference} currentBatter=CurrentBatter EXECUTE (batterAdvance=CATCHER_INTERFERENCE | batterAdvance=INTERFERENCE)) |
	({BatterAdvanceOnObstruction} currentBatter=CurrentBatter EXECUTE (batterAdvance=DECISIVE_OBSTRUCTION)) |
	({BatterAdvanceOnKWithFielderChoice} currentBatter=CurrentBatter EXECUTE (batterAdvance=K_ADVANCE_BY_FIELDER_CHOCE)) |
	({BatterAdvanceOnGdpWithFielderChoice} currentBatter=CurrentBatter EXECUTE (batterAdvance=GROUNDED_DOUBLE_PLAY_ADVANCE_WITH_FIELDER_CHOICE))
;

/**
 * Batter advance on hit
 */
Hit:
	({BatterAdvanceOnSingleHit}  currentBatter=CurrentBatter EXECUTE hit=SINGLE) |
	({BatterAdvanceOnDoubleHit}  currentBatter=CurrentBatter EXECUTE hit=DOUBLE) |
	({BatterAdvanceOnTripleHit}  currentBatter=CurrentBatter EXECUTE hit=TRIPLE) |
	({BatterAdvanceOnHomeRun}    currentBatter=CurrentBatter EXECUTE hit=HOMERUN (isEarned=('earned' | 'unEarnedForTeam'))?) |
	({BatterAdvanceOnInsidePark} currentBatter=CurrentBatter EXECUTE hit=INSIDE_PARK (isEarned=('earned' | 'unEarnedForTeam'))?)
;

/**
 * Non decisive action (not used)
 */
NoDecisiveError:
	EXTRA_ON_FLY_ERROR |
	EXTRA_ON_RECEIVE_ERROR |
	EXTRA_ON_THROW_ERROR
;

/**
 * Batter advance on decisive error action
 */
DecisiveError:
	({BatterAdvanceOnFlyError} currentBatter=CurrentBatter EXECUTE batterAdvance=REACH_ON_FLY_ERROR) |
	({BatterAdvanceOnPopError} currentBatter=CurrentBatter EXECUTE batterAdvance=REACH_ON_POP_ERROR) |
	({BatterAdvanceOnReceiveError} currentBatter=CurrentBatter EXECUTE batterAdvance=REACH_ON_RECEIVE_ERROR) |
	({BatterAdvanceOnThrowError} currentBatter=CurrentBatter EXECUTE batterAdvance=REACH_ON_THROW_ERROR) |
	({BatterAdvanceOnGdpWithError} currentBatter=CurrentBatter EXECUTE batterAdvance=GROUNDED_DOUBLE_PLAY_ADVANCE_WITH_ERROR)
;

/**
 * All case of batter out action
 */
BatterOut:
	(StrikeOut |
	FielderOut)
    notInDoublePlay ?='dontCountAsDoublePlay'?
;

/**
 * Batter out on fielding action 
 */
FielderOut:
	({BatterOutOnFlyed} currentBatter=CurrentBatter EXECUTE batterOut=FLYED_OUT) |                                 /* Checked */
	({BatterOutOnLine} 	currentBatter=CurrentBatter EXECUTE batterOut=LINE_OUT) | 									/* Checked */
	({BatterOutOnPopped} currentBatter=CurrentBatter EXECUTE batterOut=POPPED_OUT) | 								/* Checked */
	({BatterOutOnFlyedFallBall} currentBatter=CurrentBatter EXECUTE batterOut=FLYED_FALLBALL_OUT) | 				/* Checked */
	({BatterOutOnPoppedFallBall} currentBatter=CurrentBatter EXECUTE batterOut=POPPED_FALLBALL_OUT) | 				/* Checked */
	({BatterOutOnLineDriveFallBall} currentBatter=CurrentBatter EXECUTE batterOut=LINE_FALLBALL_OUT) |				/* Checked */
	({BatterOutOnGroundedBall} currentBatter=CurrentBatter EXECUTE batterOut=INT (isBunt?='b')?) |  /* grounded out  */				/* Checked */
//	{UnassistOut} 	fielderOut=UNASSIST_OUT |
	({BatterOutByRule} currentBatter=CurrentBatter EXECUTE batterOut=OUT_BY_RULE) |						/* Checked */
	({BatterOutOnAppeal} currentBatter=CurrentBatter EXECUTE batterOut=APPEAL) |
	({BatterOutOnGroundedDoublePlay} currentBatter=CurrentBatter EXECUTE batterOut=GROUNDED_DOUBLE_PLAY_OUT) |
	({BatterOutOnSacrificeFly} currentBatter=CurrentBatter EXECUTE batterOut=SACRIFICE_FLY) |
	({BatterOutOnSacrificeFlyFallBall} currentBatter=CurrentBatter EXECUTE batterOut=SACRIFICE_FLY_FOUL_BALL) |
	({BatterOutOnSacrificeHit} currentBatter=CurrentBatter EXECUTE batterOut=SACRIFICE_BUNT) |
	({BatterOutOnInfieldFly} currentBatter=CurrentBatter EXECUTE batterOut=INFIELD_FLY)
//	{DoublePlayOut} 	fielderOut=DOUBLE_PLAY |
//	{TriplePlayOut} 	fielderOut=TRIPLE_PLAY
;

/**
 * Batter out out on Strike out
 */
StrikeOut:
	({BatterOutOnSwingedStrike} currentBatter=CurrentBatter EXECUTE strikeOutValue=SWING_STRIKE_OUT)   |				/* Checked */
	({BatterOutOnLookedStrike}  currentBatter=CurrentBatter EXECUTE strikeOutValue=LOOKED_STRIKE_OUT)  |				/* Checked */
	({BatterOutOnReleasedStrike} currentBatter=CurrentBatter EXECUTE strikeOutValue=RELEASED_STRIKE_OUT)				/* Checked */
;

/**
 * Describe the current batter
 */
CurrentBatter:
	batter=BATTER ( '(' name=STRING ')' )?
;

/**
 * Describe the current runner
 */
CurrentRunner:
	runner=RUNNER ('(' name=STRING ')')?
;

/**
 * Describe a lineup
 */
LineUp:
	'lineup' teamType=TeamTypeEnum '{'
		lineUpEntries += LineUpEntry+
	'}'
;

/**
 * Describe a lineup entry
 */
LineUpEntry:
	'name' '=' name=STRING 'defensivePosition' '=' defensivePosition=LineUPAtDefensivePosition ('shirtNumber' '=' shirtNumber=INT)?  ('laterality' '=' laterality=LATERALITY)? ';'
;

/**
 * Possible line up desensive position
 */
LineUPCurrentDefensivePosition:
	INT |
	DESIGNAATED_HITTER |
	PITCH_RUNNER_WITH_POSITION |
	DESIGNATED_PITCHER |
	PITCH_HITTER_WITH_POSITION 
;

/**
 * Possible line up desensive position
 */
LineUPAtDefensivePosition:
	INT |
	DESIGNAATED_HITTER |
	PITCH_RUNNER |
	DESIGNATED_PITCHER |
	PITCH_HITTER 
;

/**
 * Type of pitch
 */
KindOfPitch:
	'b'|'B'|
	'k'|'K'|
	's'|'S'|
	'h'|'H'|
	'f'|'F'|
	'.'
;

/**
 * Roster description
 */
Roster:
	'roster' who=TeamTypeEnum name=STRING '{'
		(players += Player ( ',' players += Player)*)?
	'}'
;

/**
 * Describe a player in a roster
 */
Player:
	'player' playerName = STRING
;

/**
 * Game description structure
 */
Description:
	'header' '{'
		(
			('tournament' '=' tournament=STRING ';') &
            ('stadium' '=' stadium=STRING ';') &
            ('place' '=' place=STRING ';') &
            ('date' '=' date=STRING ';') &
            ('startTime' '=' startTime=STRING ';') &
            ('endTime' '=' endTime=STRING ';') &
            ('duration' '=' duration=STRING ';') &
            ('category' '=' category=STRING ';') &
            ('type' '=' type=GameTypeEnum ';') &
            (umpires = Umpires) &
            (scorekeepers = ScoreKeepers) &
            (notes = Notes)?
		)
	'}'
;

/**
 * Note description
 */
Notes:
	'notes' '=' notes+=STRING (',' notess+=STRING)* ';'
;

/**
 * Scorekepper description
 */
ScoreKeepers:
	'scorekeepers' '=' scorekeepers+=STRING (',' scorekeepers+=STRING)* ';'
;

/**
 * Umpire description
 */
Umpires:
	'umpires' '=' umpires+=STRING (',' umpires+=STRING)* ';'
;

/**
 * Type team 
 */ 

TeamTypeEnum:
	'visitor' | 'hometeam'
;

/**
 * Type of game
 */
GameTypeEnum:
	'baseball' | 'softball'
;


/* avances du batteur */
terminal ADVANCE_BY_OTHER_PLAYER_EROR : ADVANCE 'E' INT;
terminal SINGLE : '1B' (INT | SPECIAL_LOCATION)'b'? ADVANCE?;
terminal DOUBLE : '2B' (INT | SPECIAL_LOCATION)'b'? ADVANCE?;
terminal TRIPLE : '3B' (INT | SPECIAL_LOCATION)'b'? ADVANCE?;
terminal HOMERUN : 'HR' (INT | SPECIAL_LOCATION);
terminal INSIDE_PARK : 'IHR' (INT | SPECIAL_LOCATION);
terminal BASE_ON_BALL : 'BB';
terminal INTENTIONAL_BASE_ON_BALL : 'IBB';
terminal HIT_BY_PITCH : 'HP';
terminal BALK : 'BK'|'bk' ;
terminal GROUNDED_DOUBLE_PLAY_ADVANCE_WITH_ERROR : 'GDP' (REACH_ON_THROW_ERROR | REACH_ON_RECEIVE_ERROR);
terminal GROUNDED_DOUBLE_PLAY_ADVANCE_WITH_FIELDER_CHOICE : 'GDP' (FIELDER_CHOICE_WITH_PUTOUT | FIELDER_CHOICE);
terminal GROUNDED_DOUBLE_PLAY_OUT : 'GDP' INT;
terminal FIELDER_CHOICE: 'FC' INT ;
terminal INDIFERENCE: 'O/' INT ;
terminal FIELDER_CHOICE_WITH_PUTOUT: 'O' INT 'b'? ADVANCE? ;
terminal ADVANCE_ON_THROW: 'T' INT ;
terminal CATCHER_INTERFERENCE : 'CI' ;
terminal LOST_TURN : 'LT';
terminal K_WITH_ERROR : ('KS' REACH_ON_RECEIVE_ERROR) | ('KS' REACH_ON_THROW_ERROR);
terminal REACH_ON_FLY_ERROR : 'E' INT 'F' ADVANCE?;
terminal REACH_ON_POP_ERROR : 'E' INT 'P' ADVANCE?;
terminal REACH_ON_THROW_ERROR : INT? 'E' INT 'T' ADVANCE? 'b'?;
terminal REACH_ON_RECEIVE_ERROR : INT? 'E' INT ADVANCE? 'b'?;
terminal MUST_BE_OUT_ON_ERROR : 'EF' INT ;
terminal EXTRA_ON_FLY_ERROR : 'e' INT 'F' ADVANCE?;
terminal EXTRA_ON_THROW_ERROR : INT? 'e' INT 'T' ADVANCE?;
terminal EXTRA_ON_RECEIVE_ERROR : INT? 'e' INT ADVANCE?;
terminal FLYED_FALLBALL_OUT : 'FF' INT 'b'?;
terminal POPPED_FALLBALL_OUT : 'FP' INT 'b'?;
terminal LINE_FALLBALL_OUT : 'FL' INT ;
terminal FLYED_OUT : 'F' INT 'b'?;
terminal POPPED_OUT : 'P' INT 'b'?;
terminal LINE_OUT : 'L' INT 'b'?;
//terminal GROUND_OUT: INT ;
terminal UNASSIST_OUT : INT 'U';
terminal SWING_STRIKE_OUT : 'KS';
terminal LOOKED_STRIKE_OUT : 'KL';
terminal K_PASS_BALL : 'KSPB' ADVANCE?;
terminal K_WILD_PITCH : 'KSWP' ADVANCE?;
terminal K_ADVANCE_BY_RULE : 'KABR' ;
terminal K_ADVANCE_BY_FIELDER_CHOCE : ('KS' | 'KL') (FIELDER_CHOICE|FIELDER_CHOICE_WITH_PUTOUT) ;
terminal RELEASED_STRIKE_OUT : 'K'('S'|'L')?INT;
terminal INFIELD_FLY : 'IF' INT ;
terminal SACRIFICE_BUNT_WITH_ERROR : ('SH' REACH_ON_RECEIVE_ERROR) | ('SH' REACH_ON_THROW_ERROR) | ('SH' REACH_ON_FLY_ERROR) | ('SH' REACH_ON_POP_ERROR) ;
terminal SACRIFICE_BUNT_WITH_FIELDER_CHOICE : 'SH' (FIELDER_CHOICE|FIELDER_CHOICE_WITH_PUTOUT);
terminal SACRIFICE_BUNT : 'SH' INT;
terminal SACRIFICE_FLY_FOUL_BALL : 'FSF' INT;
terminal SACRIFICE_FLY_WITH_FIELDER_CHOICE : 'SF' (FIELDER_CHOICE|FIELDER_CHOICE_WITH_PUTOUT);
terminal SACRIFICE_FLY_WITH_ERROR : 'SF' (REACH_ON_RECEIVE_ERROR | REACH_ON_FLY_ERROR | REACH_ON_POP_ERROR);
terminal SACRIFICE_FLY : 'SF' INT;
terminal DOUBLE_PLAY : 'DP';
terminal TRIPLE_PLAY : 'TP';
terminal PASS_BALL : ('PB'|'pb') ADVANCE?;
terminal WILD_PITCH : ('WP'|'wp') ADVANCE?;
terminal ADVANCE_ON_ERROR : '('INT')' ADVANCE?;
terminal ADVANCE : '+'+;
terminal INTERFERENCE : 'INT' INT?;
terminal DECISIVE_OBSTRUCTION : 'OB' INT?;
terminal NO_DECISIVE_OBSTRUCTION : 'ob' INT?;
terminal APPEAL : 'A' INT;
terminal OUT_BY_RULE : 'OBR' (('KS' | INT) '-' INT);
terminal ADVANCE_BY_RULE : INT 'ABR' ;
terminal STOLEN_BASE : 'SB';
terminal PICK_OFF_WITH_ERROR : 'PO' (REACH_ON_RECEIVE_ERROR|REACH_ON_THROW_ERROR|EXTRA_ON_RECEIVE_ERROR|EXTRA_ON_THROW_ERROR);
terminal PICK_OFF : 'PO' INT;
terminal CAUGTH_STEALING_WITH_ERROR : 'CS' (REACH_ON_RECEIVE_ERROR|REACH_ON_THROW_ERROR|EXTRA_ON_RECEIVE_ERROR|EXTRA_ON_THROW_ERROR) ;
terminal CAUGTH_STEALING : 'CS' INT;
terminal RUNNER:  'runner' INT;
terminal BATTER: 'batter';
terminal EXECUTE: '->';
terminal LATERALITY: 'R' | 'L';

terminal PITCH_HITTER_WITH_POSITION: PITCH_HITTER INT;
terminal PITCH_RUNNER_WITH_POSITION: PITCH_RUNNER INT;
terminal DESIGNAATED_HITTER: 'dh';
terminal DESIGNATED_PITCHER: 'dp';
terminal PITCH_HITTER: 'ph';
terminal PITCH_RUNNER: 'pr';


//terminal EARNED: 'earned';
//terminal UN_EARNED_TEAM: 'unEarnedTeam';

terminal SPECIAL_LOCATION : 'LC' | 'RC' | 'LL' | 'RL' | 'LS' | 'RS' | 'MI';

//terminal DEFENSIVE_POSITION : INT | 'dh' | 'pr' | 'dp' | 'ph';

